plugins {
    id 'com.android.application'
}

apply plugin: 'android-aspectjx'

android {
    compileSdkVersion 30

    ext {
        appName = 'Naruto录音机'
        appNameEn = 'narutoRecorder'//用于创建文件夹等
    }

    defaultConfig {
        applicationId "com.naruto.narutorecorder"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 100
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "APP_NAME", "\"${appNameEn}\""

        // xml布局文件调用
        resValue "string", "version_name", "${defaultConfig.versionName}"
        resValue "string", "version_code", "${defaultConfig.versionCode}"
        resValue "string", "app_name", "${appName}"
    }

    //签名
    signingConfigs {
        config {
            storeFile file(KEY_PATH)
            storePassword KEY_PW
            keyAlias KEY_ALIAS
            keyPassword ALIAS_PW
        }
    }


    buildTypes {
        debug {
            minifyEnabled false//代码混淆
            shrinkResources false//资源压缩
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOG_DEBUG", "true"
            signingConfig signingConfigs.config
        }

        release.initWith(debug)
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release" && output.outputFile != null && output.outputFileName.endsWith('.apk')) {
                output.outputFileName = "${appName}_v${android.defaultConfig.versionName}_${new Date().format("yyyyMMdd_HHmmss")}.apk"
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0-alpha02'

    api 'org.aspectj:aspectjrt:1.9.6'
}